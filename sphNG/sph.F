      PROGRAM sph
c************************************************************
c************************************************************
c************************************************************
c******************                    **********************
c******************       sphNG        **********************
c******************                    **********************
c************************************************************
c************************************************************
c************************************************************
c                                                           *
c  sphNG is a three-dimensional hydro, MHD and radiation    *
c  hydro code based on the Smoothed Particle Hydrodynamics  *
c  method.                                                  *
c                                                           *
c************************************************************
c                                                           *
c  LICENSE                                                  *
c                                                           *
c  Copyright (c) 2007-2020 Matthew Bate and contributors    *
c     (see below for author information).                   *
c                                                           *
c  sphNG is free software; you can redistribute it and/or   *
c  modify it under the terms of the GNU General Public      *
c  License as published by the Free Software Foundation;    *
c  either version 3 of the License, or (at your option)     *
c  any later version, supplemented by the following two     *
c  conditions under section 7c of GPLv3:                    *
c                                                           *
c  1) Currently, there is no single code paper that         *
c     describes the algorithms, capabilities, and           *
c     performance of the sphNG code.                        *
c                                                           *
c     In the absence of such a code paper,                  *
c     the following papers should be cited in scientific    *
c     publications using the code:                          *
c                                                           *
c     Benz W., Bowers R.L., Cameron A.G.W., Press W.H.,     *
c          1990, ApJ, 348, 647                              *
c     Bate M.R., Bonnell I.A., Price, N.M., 1995,           *
c          MNRAS, 277, 362                                  *
c     Price D.J., Monaghan J.J, 2007, MNRAS, 374, 1347      *
c                                                           *
c     The above papers, and those cited by the above papers,*
c     describe the fundamental hydrodynamical and           *
c     gravitational algorithms implemented in sphNG.        *
c                                                           *
c     In addition:                                          *
c                                                           *
c     If the flux-limited diffusion radiation transport     *
c     is used, the following papers should be cited:        *
c                                                           *
c     Whitehouse S.C., Bate M.R., Monaghan J.J., 2005,      *
c          MNRAS, 364, 1367                                 *
c     Whitehouse S.C., Bate M.R., 2006, MNRAS, 367, 32      *
c                                                           *
c     If magnetohydrodynamics (MHD) is used, these papers   *
c     should be cited:                                      *
c                                                           *
c     Price D.J., Monaghan J.J, 2005, MNRAS, 364, 384       *
c     Tricco T., Price D.J., Bate M.R., 2016, J. Comp.      *
c          Phys., 322, 326                                  *
c                                                           *
c     If non-ideal MHD is used, these papers should be      *
c          cited:                                           *
c                                                           *
c     Wurster J., Price D.J., Ayliffe B.A., 2014, MNRAS,    *
c          444, 1104                                        *
c     Wurster J., Price D.J., Bate M.R., 2016, MNRAS,       *
c          457, 1037                                        *
c                                                           *
c     If the NICIL library is used for non-ideal MHD        *
c          calculations, these papers should be cited:      *
c          Wurster J., 2016, PASA, 33, 41                   *
c          Wurster J., 2021, MNRAS, 501:5873-5891           *
c                                                           *
c  2) Any redistribution of substantial fractions of the    *
c     code as a different project should preserve the name  *
c     "sphNG" in the name of the code (in addition to the   *
c     GPLv3 condition that this copyright notice be         *
c     retained both here and in the source file headers) to *
c     prohibit misrepresentation of a redistribution as     *
c     entirely new work.                                    *
c                                                           *
c  sphNG is distributed WITHOUT ANY WARRANTY; without even  *
c  the implied warranty of MERCHANTABILITY or FITNESS       *
c  FOR A PARTICULAR PURPOSE.                                *
c  See the GNU General Public License for more details.     *
c                                                           *
c  M.R.Bate@exeter.ac.uk, 7th July 2020                     *
c                                                           *
c                                                           *
c************************************************************
c                                                           *
c  MAIN AUTHORS                                             *
c                                                           *
c  W. Benz        Los Alamos National Laboratory 31/08/86   *
c                 Harvard College Observatory    10/03/87   *
c                                                           *
c  W. H. Press    Harvard University             21/11/86   *
c                                                           *
c  I. A. Bonnell  University de Montreal         XX/XX/91   *
c                 University of Cambridge, UK    01/10/92   *
c                                                           *
c  M. R. Bate     University of Cambridge, UK    01/10/92   *
c                 University of Exeter, UK       01/03/01   *
c                                                           *
c  D. J. Price    University of Cambridge, UK    XX/XX/03   *
c                 University of Exeter, UK       XX/XX/05   *
c                 Monash University, Australia   XX/XX/11   *
c                                                           *
c  B. A. Ayliffe  University of Exeter, UK       20/12/06   *
c                                                           *
c  B. T. Lewis    University of Exeter, UK       XX/XX/13   *
c                                                           *
c  J. Wurster     University of Exeter, UK       XX/XX/15
c                                                           *
c  Other authors up until July 2020 include:                *
c     S.C. Whitehouse, B.A. Ayliffe, C.L. Dobbs,            *
c     D.M. Acreman, F, Meru, T.S. Tricco, A.K. Young,       *
c     M.O. Jones, S. Glover, M.D. Smith                     *
c                                                           *
c  See the git log for more recent authors.                 *
c                                                           *
c************************************************************
c                                                           *
c  Modifications by: I. A. Bonnell      UdeM    28/05/92    *
c                                                           *
c  (1) random number generator from Numerical Recipes: ran1 *
c  (2) cylindrical boundaries (ibound = 2): ghosp2          *
c  (3) gforsa and gforsn are parallelized                   *
c  (4) generator of axial density perturbations (ibound = 2)*
c                                                           *
c  Modifications by: M. R. Bate UofCam, MPIA, Exeter 1995-  *
c                                                           *
c  (1) individual particle timesteps                        *
c  (2) inclusion of accreting, massive point masses (sink   *
c         particles) with boundaries, identified by iphase. *
c  (3) corrected ghost boundaries                           *
c  (4) constant pressure boundaries                         *
c  (5) OpenMP parallelisation (1999/2000)                   *
c  (6) Flux-limited diffusion radiative transfer (2005)     *
c  (7) MPI parallelisation (2007)                           *
c  (8) Thermochemical interstellar medium model (2013-2015) *
c  (9) Implicit dust drag (2015)                            *
c  (10) One-fluid dust (2017)                               *
c  (11) Stellar feedback and ionisation (2018)              *
c                                                           *
c  Modifications by: D. J. Price       UofExeter 2003-2006  *
c                                                           *
c  (1) grad h terms, grad soft terms                        *
c  (2) MHD                                                  *
c  (3) additional smoothing kernels                         *
c                                                           *
c  Modifications by: B. A. Ayliffe     UofExeter 2006-2012  *
c                                                           *
c  (1) modelling protoplanets in discs, planet surfaces     *
c  (2) two-fluid dust/planetesimals                         *
c                                                           *
c  Modifications by: B. T. Lewis       UofExeter 2013-      *
c                                                           *
c  (1) `average-h' MHD method (see Lewis et al 2015a,b)     *
c         Now deprecated: see erratum to LBP2015b           *
c  (2) implemented modified div B cleaning method from      *
c         Tricco, et al. (2016)                             *
c  (3) many, many, code cleanups, modernisations, and       *
c         miscellaneous improvements                        *
c                                                           *
c  Modifications by: J. Wurster  UofExeter, UofStAnd 2016-  *
c                                                           *
c  (1) non-ideal MHD                                        *
c                                                           *
c************************************************************
c  General Description                                      *
c                                                           *
c  This code allows three different type of tasks to be     *
c  done according to variable "job" . Variable job is read  *
c  from the command line.                                   *
c                                                           *
c  1) job=initial                                           *
c     -----------                                           *
c     this allows to compute new initial conditions. This   *
c     can be done by either starting from scratch or by     *
c     adding two existing situations together               *
c     (see subroutine newrun)                               *
c                                                           *
c  2) job=transfer                                          *
c     -------------                                         *
c     this allows to copy one or several dumps from one     *
c     file to another one. During this transfer  there is   *
c     a possibility of making changes in the various        *
c     quantities                                            *
c     (see subroutine transfd)                              *
c                                                           *
c  3) job=evolution                                         *
c     -------------                                         *
c     this allows to compute the actual evolution of the    *
c     system by specifying the various forces to be         *
c     included                                              *
c     (see subroutine evol)                                 *
c                                                           *
c************************************************************
c                                                           *
c  Compiling sphNG                                          *
c  ===============                                          *
c                                                           *
c  File : idim                                              *
c     The value 'idim' must be set >= number of particles   *
c        per MPI process that are required                  *
c     Options for the inclusion of different physical       *
c        processes must be turned on or off                 *
c                                                           *
c  File : unit.f                                            *
c     Code units need to be defined (mass and length)       *
c                                                           *
c  A Makefile is provided.  After setting compiler info     *
c     compilation typically takes the form:                 *
c                                                           *
c     make mpi=no openmp=no gradhrk                         *
c                                                           *
c     for an executable without radiative transfer.         *
c     yes, or no, are used in the above command to turn     *
c     on, or off, MPI parallelisation and OpenMP.           *
c                                                           *
c     To include radiative transfer (requires additional    *
c     external table files)                                 *
c                                                           *
c     make mpi=no openmp=no gradhrkrt                       *
c                                                           *
c************************************************************
c                                                           *
c  Running sphNG                                            *
c  =============                                            *
c                                                           *
c  The main option of the code, JOB, is read from the       *
c  command line. If JOB=INITIAL, TRANSFER, EXTRACT, PLANET, *
c  MAKEDUST, or MAKEPLOT the code goes into an              *
c  interactive mode. Just answer the questions after having *
c  read the informations below.                             *
c  If EVOLUTION is chosen, the code looks for a file        *
c  where the various options have to be defined.            *
c                                                           *
c  Files used and written by the code:                      *
c  -----------------------------------                      *
c                                                           *
c  1) If JOB=INITIAL and the SCRATCH option is chosen, then *
c     the code uses the 'inspho' file and the answeres to   *
c     questions to produce a binary file containing all the *
c     quantities needed for the evolution, and an ASCII     *
c     file containing parameters to control the evolution.  *
c  2) If JOB=INITIAL and the EXIST option is chosen, then   *
c     the code will require two files containing data to    *
c     combine them into a third file.                       *
c  3) JOB=PLANET is similar to JOB=INITIAL, but is          *
c     specifically used to set up planets embedded in discs *
c  4) If JOB=TRANSFER, EXTRACT, MAKEDUST, or MAKEPLOT the   *
c     code needs an existing binary file and upon           *
c     completion it will create another one. The code may   *
c     also need the ASCII parameters file.                  *
c  5) If JOB=EVOLUTION the code needs a binary file with    *
c     initial conditions produced by the INITIAL option     *
c     or the file resulting from a prior evolution run.     *
c     The ASCII parameters file is continually updated      *
c     during the evolution. An ASCII output file is also    *
c     created that contains a variety of information        *
c     regarding the status of the evolution.                *
c     It is important to check the file periodically and    *
c     monitor the total energy and angular momentum for     *
c     example since both should be conserved (within some   *
c     tolerance).                                            *
c                                                           *
c  Various important details                                *
c  -------------------------                                *
c  1) The code assumes that the name of the binary file     *
c     is of the form XXXXXYY. XXXXX can be being either     *
c     characters or digits and YY digits (typically 00 or 01*
c     at the start). When a new file is created, its name   *
c     will be XXXXXYY+1 (see subroutine FILE)               *
c  2) The code checks periodically for a file called        *
c     'message'. This file permits to change some of the    *
c     options defined in the input deck during the code's   *
c     execution or stops the code cleanly (see MESOP)       *
c  3) The debug option IDEBUG should be used with care since*
c     it produces a lont of output! ITRACE can be set to all*
c     to trace the code through the various subroutines in  *
c     case of mysterious errors!                            *
c  4) The code typically assumes a perfect gas equation     *
c     of state (see EOSPG). This can be complicated         *
c     at will.                                              *
c  5) The maximum number of particles that can be used      *
c     by a single MPI process is determined by the          *
c     IDIM (set in the 'idim' file. The number of particles *
c     NPART has to be less (or equal) to IDIM.              *
c  6) Before anything can be done with the code, the proper *
c     units have to be defined ('unit.f'). The numbers to   *
c     input are the total mass and a length scale. These    *
c     numbers are actually inside the code and not options  *
c     since for any particular choice of units, they should *
c     not be changed anymore! Once both numbers have been   *
c     coded in, the code determines all other units.        *
c                                                           *
c                                                           *
c-----------------------------------------------------------*
c                                                           *
c  job=evolution input deck (see options.F)                 *
c  --------------------------------------                   *
c                                                           *
c  namerun  = name of the run                               *
c  file1    = name of file containing starting dump and on  *
c             which the file names of new dumps based       *
c  varsta   = entropy : use entropy as variable of state    *
c           = intener : use specific internal energy as     *
c                       variable of state                   *
c  varmhd   = Brho : magnetic field variable is B/rho       *
c           = Bvol : magnetic field variable is B           *
c  encal    = equation of state (i,a,p,v,r,c,x,m,t)         *
c             when varsta = intener,  see: eospg.F          *
c                                                           *
c  --------------                                           *
c                                                           *
c  mpitype  = Type of MPI decomposition                     *
c             (s)phere, (d)isc, (c)artesian, b(inary)       *
c             May also use 4 numbers to define origin       *
c             Default:  d 0.0 0.0 0.0 0.0                   *
c                                                           *
c  initialptm = if sink particles exist in binary file,     *
c             what type they are (0 = not exist)            *
c  iaccevol = Variable, Roche-Sep, or Fixed Accretion Radii *
c             (v/s/f).  Default: f                          *
c  iptmass  = if sink particles created dynamically, what   *
c             type to create                                *
c                                                           *
c  --------------                                           *
c  igrp     = 0       : no pressure gradients               *
c             1       : with pressure gradients             *
c  igphi    = 0       : no self-gravity                     *
c             1       : with self-gravity                   *
c           = -1      : no back reaction on sink particles  *
c  ifsvi    = 0       : no artificial viscosity             *
c           >=1       : with artificial viscosity           *
c                     : artificial resistivity also defined *
c  ndivBsubcycles,overclean_fac,iresist,etamhd =            *
c                     parameters for MHD                    *
c  ifcor    = 0       : no coriolis forces                  *
c             1       : with coriolis forces                *
c  ichoc    = 0       : no heating due to artif. viscosity  *
c             1       : with heating due to artif. viscosity*
c  iener    = 0       : no total energy conservation        *
c             1       : with total energy conservation      *
c  damp     = value of general damping coefficient          *
c  ibound   = 0       : no boundaries                       *
c             1       : with reflective boundaries          *
c                     : many other options                  *
c  iexf     = 0       : no external forces                  *
c             1       : allow for external forces           *
c  iexpan   = 0       : no general expansion                *
c             1       : general expansion superposed        *
c                                                           *
c  the external force, the boundaries and the general       *
c  damping are NOT considered in the energy equation.       *
c                                                           *
c  --------------                                           *
c  nstep    = frequency at which timesteps are written on   *
c             disk                                          *
c  nfullstep= frequency at which full dumps are written     *
c  iptoutnum= frequency at which sink particles information *
c             is written to disk (no. of times per large    *
c             timestep)                                     *
c  tdump    = time between dumps (alternative to number     *
c             of steps between dumps)                       *
c  tol, tolptm, tolh = tolerance used by the Runge-Kutta    *
c             for gas, sink particles (last not used)       *
c  irec     = binary dump counter (value does not matter)   *
c  tmax     = max time in minute allowed for the job        *
c  tstop    = time (code unit) at which the program         *
c             will stop.                                    *
c  dtmax    = largest timestep & minimum time between dumps *
c  dtini    = initial timestep when using t=0 dump file     *
c                                                           *
c  only if ifcor=1                                          *
c  ---------------                                          *
c  omeg0     =  angular velocity of the reference frame in  *
c               1/s                                         *
c                                                           *
c  only if iexpan= 1                                        *
c  -----------------                                        *
c  vexpan    =  expansion velocity of the frame of ref. in  *
c               cm/s.                                       *
c                                                           *
c  various parameters required for different ibound values  *
c  see options.F for further details                        *
c  ----------------                                         *
c                                                           *
c  Minimum is:                                              *
c                                                           *
c  rmind     =  possible value for inner radial boundary    *
c  rmax      =  max. radius for spherical boundary          *
c  xmin,xmax =  min and max x coord. for boundary           *
c  ymin,ymax =  min and max y coord. for boundary           *
c  zmin,zmax =  min and max z coord. for boundary           *
c                                                           *
c                                                           *
c************************************************************
c                                                           *
c  Incomplete alphabetical list of common variables         *
c  ================================================         *
c                                                           *
c  variable  common block   description                     *
c- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -*
c                                                           *
c  alpha        (shock) : coefficient for bulk viscosity    *
c  angto        (angm ) : total angular momentum            *
c  angx         (angm ) : x componant of total angular mom. *
c  angy         (angm ) : y componant of total angular mom. *
c  angz         (angm ) : z componant of total angular mom. *
c  beta         (shock) : coefficient for Neumann-Richtmeyer*
c                         viscosity                         *
c  cmx1         (out1 ) : x coord. of c.m. body 1           *
c  cmy1         (out1 ) : y coord. of c.m. body 1           *
c  cmz1         (out1 ) : z coord. of c.m. body 1           *
c  cmx2         (out2 ) : x coord. of c.m. body 2           *
c  cmy2         (out2 ) : y coord. of c.m. body 2           *
c  cmz2         (out2 ) : z coord. of c.m. body 2           *
c  cnormk       (kerne) : normalisation constant of kernel  *
c  damp         (dissi) : general damping coefficient       *
c  dq(idim)     (ener1) : heating due to shock dissipation  *
c  dvtable      (table) : spacing between table values      *
c  encal        (cgas ) : eos/energy calculation method flag*
c  escap        (fracg) : mass of escapors                  *
c  file1        (file ) : name of file number 1             *
c  file2        (file ) : name of file number 2             *
c  file3        (file ) : name of file number 3             *
c  fmass(itable)(table) : mass of particle as a funct. of r *
c  fmas1        (bodys) : fraction of total mass in body 1  *
c  fmas2        (bodys) : fraction of total mass in body 2  *
c  fnbtot       (units) : number of total mass              *
c  fpoten(itable)(table): potential energy as a funct. of r *
c  gamma        (cgas ) : value of gamma                    *
c  grwij(itable)(table) : gradient of kernel table          *
c  gt           (gtime) : current global time in code units *
c  hma1         (out1 ) : max h for body 1                  *
c  hma2         (out2 ) : max h for body 2                  *
c  hmi1         (out1 ) : min h for body 1                  *
c  hmi2         (out2 ) : min h for body 2                  *
c  ibound       (typef) : flag to allow boundaries          *
c  ichoc        (typef) : flag to allow shock heating       *
c  idebug       (debug) : debug option                      *
c  idisk1       (logun) : logical unit number for disk file *
c  idisk2       (logun) : logical unit number for disk file *
c  idisk3       (logun) : logical unit number for disk file *
c  idist        (new  ) : type of distribution for setting  *
c                         the initial particles             *
c  idumy        (random): number to initialize generator    *
c  iener        (typef) : flag to allow total energy cons.  *
c  iexf         (typef) : flag to allow external forces     *
c  iexpan       (typef) : flag to allow expansion           *
c  ifcor        (typef) : flag to allow coriolis forces     *
c  ifsvi        (typef) : flag to allow arti. viscosity     *
c  igrp         (typef) : flag to allow pressure gradients  *
c  igphi        (typef) : flag to allow body forces         *
c  iprint       (logun) : logical unit number for print out *
c  irec         (recor) : record number on disk             *
c  iterm        (logun) : logical unit number for read      *
c  itrace       (debug) : flag to allow flow tracing        *
c  job          (actio) : main option that tells the code   *
c                         what option it has to run         *
c  n1           (bodys) : number of particles in body 1     *
c  n2           (bodys) : number of particles in body 2     *
c  namerun      (actio) : name of the run                   *
c  ncount       (tming) : number of timestep since last dump*
c                         was written on disk               *
c  np           (new  ) : number of particles at initial.   *
c  npart        (part ) : total number of particles         *
c  nstep        (tming) : number of  timestep between disk  *
c                         dumps                             *
c  tnext        (tming) : time of next dump                 *
c  omeg0        (rotat) : angular velocity of frame of ref. *
c  pdv(idim)    (ener1) : pdv work on particle              *
c  poten(idim)  (ener1) : potential energy of particle      *
c  pr(idim)     (eosq ) : pressure at particle locations    *
c  rho(idim)    (densi) : density at particle locations     *
c  rmax         (rbnd ) : max radius of boundary            *
c  romax1       (out1 ) : max density in body 1             *
c  romax2       (out2 ) : max density in body 2             *
c  romean1      (out1 ) : mean density body 1               *
c  romean2      (out2 ) : mean density body 2               *
c  tgrav        (ener2) : total gravitational energy        *
c  thermal      (new  ) : either entropy or int. energy at  *
c                         initialisation                    *
c  tkin         (ener2) : total kinetic energy              *
c  tmax         (tming) : maximum time allowed for the job  *
c  totm         (new  ) : total mass represented by part.   *
c  trot         (ener2) : total rotational energy           *
c  tstep        (tming) : cpu time needed for one timestep  *
c  tstop        (tming) : time at which simulation has to   *
c                         stop (code units)                 *
c  tterm        (ener2) : total thermal energy              *
c  udens        (units) : unit of density                   *
c  udist        (units) : unit of length                    *
c  uergg        (units) : unit of energy per unit mass      *
c  uergcc       (units) : unit of pressure                  *
c  umass        (units) : unit of mass                      *
c  utime        (units) : unit of time                      *
c  varsta       (varet) : variable of state beside density  *
c  vcmx1        (out1 ) : x comp. of velocity of cm body 1  *
c  vcmy1        (out1 ) : y comp. of velocity of cm body 1  *
c  vcmz1        (out1 ) : z comp. of velocity of cm body 1  *
c  vcmx2        (out2 ) : x comp. of velocity of cm body 2  *
c  vcmy2        (out2 ) : y comp. of velocity of cm body 2  *
c  vcmz2        (out2 ) : z comp. of velocity of cm body 2  *
c  vexpan       (expan) : expansion velocity                *
c  vsmax        (eosq ) : maximum sound speed               *
c  vsound(idim) (eosq ) : sound speed at particle locations *
c  vvx1         (new  ) : x componant of velocity of center *
c                         of mass body 1                    *
c  vvx2         (new  ) : x componant of velocity of center *
c                         of mass body 2                    *
c  vvy1         (new  ) : y componant of velocity of center *
c                         of mass body 1                    *
c  vvy2         (new  ) : y componant of velocity of center *
c                         of mass body 2                    *
c  vvz1         (new  ) : z componant of velocity of center *
c                         of mass body 1                    *
c  vvz2         (new  ) : z componant of velocity of center *
c                         of mass body 2                    *
c  vxyzu(4,idim)(part ) : x,y,z componants of the velocity  *
c                         and internal energy of particle   *
c  wij(itable)  (table) : table of kernel values            *
c  xyzmh(5,idim)(part ) : x,y,z coordinates of the particles*
c                         and mass and h of the particles   *
c  xmax         (rbnd ) : position of upper reflec. bound.  *
c  xmin         (rbnd ) : position of lower reflec. bound.  *
c  xx1          (new  ) : x coordinate of center of mass    *
c                         body 1                            *
c  xx2          (new  ) : x coordinate of center of mass    *
c                         body 2                            *
c  ymax         (rbnd ) : position of upper reflec. bound.  *
c  ymin         (rbnd ) : position of lower reflec. bound.  *
c  yy1          (new  ) : y coordinate of center of mass    *
c                         body 1                            *
c  yy2          (new  ) : y coordinate of center of mass    *
c                         body 2                            *
c  zmax         (rbnd ) : position of upper reflec. bound.  *
c  zmin         (rbnd ) : position of lower reflec. bound.  *
c  zz1          (new  ) : z coordinate of center of mass    *
c                         body 1                            *
c  zz2          (new  ) : z coordinate of center of mass    *
c                         body 2                            *
c                                                           *
c************************************************************
c                                                           *
c  timing and memory requirements                           *
c  ==============================                           *
c                                                           *
c  Both vary enormously with the various options chosen.    *
c  Running time scales like NlogN.                          *
c                                                           *
c************************************************************
#ifdef _OPENMP
      USE OMP_LIB, ONLY: OMP_LOCK_KIND, OMP_INIT_LOCK
#endif
#ifdef MPIALL
#include "mpi_sup.h"
#endif
      INCLUDE 'idim'
      INCLUDE 'igrape'

#ifdef MPIALL
      INCLUDE 'COMMONS/mpiall'
      INCLUDE 'COMMONS/mpidebug'
#endif
#ifdef MPI
      INCLUDE 'COMMONS/mpi'
#endif

      INCLUDE 'COMMONS/infor'
      INCLUDE 'COMMONS/actio'
      INCLUDE 'COMMONS/debug'
      INCLUDE 'COMMONS/savernd'
      INCLUDE 'COMMONS/rbnd'
      INCLUDE 'COMMONS/logun'

#ifdef _OPENMP
      INCLUDE 'COMMONS/openmp_locks'
#endif

#ifdef ZEN
      INTEGER (4)        :: clist(8)
      INTEGER :: mpi_ppn, omp_get_thread_num,omp_get_num_threads
#endif
#ifdef ZEN
      clist(1:8) = (/ 0, 1, 4, 5, 2, 3, 6, 7 /)
#endif
#ifdef MPIALL
      iloop = 0
      CALL MPI_INIT(ierr)
      CALL MPI_COMM_RANK(MPI_COMM_WORLD, iproc, ierr)
      CALL MPI_COMM_SIZE(MPI_COMM_WORLD, numproc, ierr)
#ifdef ZEN
!$OMP PARALLEL
!$OMP.  DEFAULT (none)
!$OMP.  SHARED  (iproc, clist)
!$OMP.  PRIVATE (j,mpi_ppn)
      mpi_ppn=8/omp_get_num_threads() 
      j = MOD (iproc, mpi_ppn) + omp_get_thread_num() * mpi_ppn 
      if (iproc==0 .or. iproc == 1) print*,'iproc j mpi_ppn',iproc,j,
     .mpi_ppn
      CALL place_me (j, clist)
!$OMP END PARALLEL        
#endif
      IF (numproc.GT.nummaxproc) THEN
         WRITE (*,*) 'ERROR - numproc.GT.nummaxproc'
         CALL quit(1)
      ENDIF
      DO i = 1, idim
         lblocklengths(i) = 1
      END DO
#else
#ifdef ZEN
!$OMP PARALLEL
!$OMP.  DEFAULT (none)
!$OMP.  SHARED  (clist)
!$OMP.  PRIVATE (j)
      j = omp_get_thread_num()
      CALL place_me (j, clist)
!$OMP END PARALLEL      
#endif
#endif
#ifdef MPI
      IF (idim_MPI.NE.idim) THEN
         PRINT *,'ERROR - idim_MPI.NE.idim'
         CALL quit(0)
      ENDIF
#endif
#ifdef MPIDEBUG
#ifndef MPIALL
      PRINT *,'ERROR - MPIDEBUG=yes but not an MPI build'
      CALL quit(0)
#endif
#endif
c
c--Version history for the code, prior to use of SVN / GIT repository
c   Date is date last updated with other code version changes
c
c      version = 'SPRMHD-MPI-1.0-MRB-DJP-16-05-06'
c
c   Derived from version = 'SPRMHD-1.1-MRB-DJP-12-01-06'
c     but with MPI/OpenMP hybrid capability.
c
c      version = 'SPRMHD-1.1-MRB-DJP-12-01-06'
c
c   Derived from version = 'SPRMHD-1.0-MRB-DJP-06-09-05'
c     but with MHD looking like it kind of, maybe, works.
c
c      version = 'SPRMHD-1.0-MRB-DJP-06-09-05'
c
c   Derived from version = 'SPH3DT-PTMAS-GRAPE-O4-1.0-MRB-17-04-98'
c     but including MHD, gradh terms and radiative transfer.
c
c      version = 'SPH3DTSPGO4P-OPT6-1.0-MRB-29-06-05'
c
c   Derived from version = 'SPH3DT-PTMAS-GRAPE-O4-1.0-MRB-17-04-98'
c     but has been rewritten to have arrays such as x,y,z,pmass,h combined
c     into xyzmh(5,*) etc for better cache useage.  Also, has leapfrog
c     integrator, new tree opening criteria, sink particle boundaries have
c     been removed and the new code has approximately half of the memory foot
c     print of the old code.
c
c   Same as version ='SPH3DT-PTMAS-GRAPE-3.0-MRB-20-02-97' (sph3DTSP_GRAPE3_O4)
c      but includes boundary types 90 and 91 and new grape_main_split.f
c      from sph3DTSP_GRAPE3.  
c      Similar to sph3DTSP_GRAPE3 but includes variable type of 
c      Kernel (3rd and 4th order kernels) from sph3DTSP_GRAPE3_O4.
c      ALSO, can be compiled with either single or double precision.
c
c   Similar to version 'SPH3DT-PTMAS-GRAPE-1.0-MRB-27-04-96'
c
c   Same as version 'SPH3D-TIMESTEPS-PTMAS-5.03-MRB-01-06-95' (infall8)
c      but with TREE/GRAPE switchable computation.  Similar to
c      version 'SPH3D-TIMESTEPS-PTMAS-5.01-MRB-01-02-95'
c
c   Same as version SPH3D-TIMESTEPS-PTMAS-4.01-MRB-30-01-94,
c     but tidied up - all unused stuff thrown away (old boundaries,
c     excessive memory use etc).
c
c   Same as version SPH3D-TIMESTEPS-PTMAS-3.01-MRB-16-05-94,
c     with boundaries as used by Bate,Bonnell,Price 1995
c     but compiled under FORTRAN 90.
c
c
c--Floating point number error checking (DCR C routine)
c
c      CALL floating
c
c--Read main main option for run
c
      IF (imhd.EQ.idim .AND. imhdxsph.NE.imhd) THEN
         WRITE (*,*) 'ERROR - imhd, imhdxsph ',imhd, imhdxsph
         CALL quit(0)
      ELSEIF (XSPH .AND. imhdxsph.NE.idim) THEN
         WRITE (*,*) 'ERROR - XSPH, imhdxsph ',XSPH, imhdxsph
         CALL quit(0)
      ENDIF
      IF (idustRT.LT.0 .OR. idustRT.GT.1) THEN
         WRITE (*,*) 'ERROR - idustRT'
         CALL quit(0)
      ENDIF
      IF (ioptimise_column.LT.0 .OR. ioptimise_column.GT.1 .OR.
     &     (ioptimise_column.EQ.1 .AND. idustRT.NE.1)) THEN
         WRITE (*,*) 'ERROR - ioptimise_column ',ioptimise_column
         CALL quit(0)
      ENDIF
      IF (imakedust .AND. idustIMPL.NE.1) THEN
         WRITE (*,*) 'ERROR - if imakedust, then need idustIMPL=1'
         CALL quit(0)
      ENDIF

#ifdef _OPENMP
      DO i = 1, idim2/10+1
         CALL OMP_INIT_LOCK(forcei_lock(i))
      END DO
      CALL OMP_INIT_LOCK(revtree_lock)
#endif
      nlistinactive = 0

c
c--Get main option of job from command line argument
c
      CALL getcom(job, inname)

c
c--Set output and input unit numbers
c
      IF (job(1:9).NE.'evolution') THEN
        iprint = istdout 
      ELSE
        iprint = 9
      ENDIF

#ifdef MPIALL
      IF (iproc.NE.0) THEN
         iprint = 40+iproc
      ENDIF
      iread = 10
#else
      iread = istdin
#endif
c
c--Define code's units
c
      CALL unit
c
c--Initialise starting date and time
c  (some versions of the zz file require this)
c
      CALL getdat(iday, imon, iyear)
c
c--Initialise random number generator arrays
c
      iyr = 0
      DO i = 1, NTAB
         iv(i) = 0
      END DO
c
c--Execute main option
c
c  1) define new initial conditions
c
      IF (job(1:7).EQ.'initial') CALL newrun
c
c  2) transfer dumps to another file
c
c      IF (job(1:8).EQ.'transfer') CALL transfd
c
c  3) integrate the system
c
      IF (job(1:9).EQ.'evolution') THEN
         IF (imakedust) THEN
            WRITE (*,*) 'ERROR - cannot evolve with imakedust'
            CALL quit(0)
         ENDIF
         CALL evol
      ENDIF
c
c  4) reduce output files
c
      IF (job(1:6).EQ.'reduce') CALL reduce
c
c  5) extract an output file
c
      IF (job(1:7).EQ.'extract') CALL extract
c
c  6) setup planet calculations
c
      IF (job(1:7).EQ.'planet') CALL newplanet
c
c  7) add dust to existing dump file (e.g. relaxed disc)
c
      IF (job(1:8).EQ.'makedust') CALL makedust
c
c  8) add dust to existing dump file (e.g. relaxed disc)
c
      IF (job(1:8).EQ.'makeplot') CALL makeplot

      WRITE(*,*)' Unknown command'
      CALL quit(0)
      END
